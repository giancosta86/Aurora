/*ยง
  ===========================================================================
  Aurora
  ===========================================================================
  Copyright (C) 2015 Gianluca Costa
  ===========================================================================
  Licensed under the Apache License, Version 2.0 (the "License");
  you may not use this file except in compliance with the License.
  You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
  ===========================================================================
*/

buildscript {
    repositories {
        maven {
            url "https://dl.bintray.com/giancosta86/Hephaestus"
        }
    }

    dependencies {
        classpath "info.gianlucacosta.moonlicense:moonlicense-gradle:1.0"
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.1'
}


apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: "info.gianlucacosta.moonlicense"

group = 'info.gianlucacosta.aurora'
archivesBaseName = 'aurora'
version = '0.9'


description = 'Elegant Gradle projects for GitHub and Bintray'


repositories {
    mavenLocal()

    maven {
        url "https://dl.bintray.com/giancosta86/Hephaestus"
    }

    jcenter()
}


dependencies {
    compile gradleApi()

    compile localGroovy()

    testRuntime 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
    testRuntime 'info.gianlucacosta.moonlicense:moonlicense-gradle:1.0'
}




task('sourcesJar', type: Jar, dependsOn: 'classes') {
    classifier = 'sources'
    from project.sourceSets.main.allSource
}


task('groovydocJar', type: Jar, dependsOn: 'groovydoc') {
    classifier = 'javadoc'
    from project.groovydoc.destinationDir
}


artifacts {
    archives project.sourcesJar
    archives project.groovydocJar
}



moonLicense {
    license = apache2

    productInfo {
        productName = "Aurora"
        inceptionYear = 2015
        copyrightHolder = "Gianluca Costa"
    }
}


uploadArchives {
    repositories {
        mavenDeployer {
            repository(
                    url: "file://" + new File(project.buildDir, "mavenDeploy")
            )

            pom.project {
                name project.name
                description project.description


                url 'https://github.com/giancosta86/Aurora'


                developers {
                    developer {
                        name 'Gianluca Costa'
                        email 'gianluca@gianlucacosta.info'
                    }
                }

                scm {
                    url 'git@github.com:giancosta86/Aurora'
                    connection 'scm:git:git@github.com:giancosta86/Aurora.git'
                    developerConnection 'scm:git:git@github.com:giancosta86/Aurora.git'
                }

            }
        }
    }
}



bintray {
    if (project.hasProperty("bintrayKey")) {
        user = bintrayUser
        key = bintrayKey
    }

    filesSpec {
        from('build/mavenDeploy') {
            include "**/${project.version}/*.jar"
            include "**/${project.version}/*.pom"
        }
        into '.'
    }

    dryRun = false
    publish = false

    pkg {
        repo = 'Hephaestus'

        name = project.name
        desc = project.description

        websiteUrl = 'https://github.com/giancosta86/Aurora'
        issueTrackerUrl = 'https://github.com/giancosta86/Aurora/issues'
        vcsUrl = 'https://github.com/giancosta86/Aurora.git'

        licenses = ['Apache-2.0']
        labels = ['elegant', 'projects', 'Gradle', 'GitHub', 'Bintray']

        publicDownloadNumbers = false

        version {
            name = project.version
            vcsTag = 'v' + project.version

            released = new Date()

            gpg {
                sign = true
            }

            mavenCentralSync {
                sync = false
            }
        }
    }
}

